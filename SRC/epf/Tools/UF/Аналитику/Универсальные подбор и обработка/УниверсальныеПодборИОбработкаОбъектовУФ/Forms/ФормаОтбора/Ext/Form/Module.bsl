// Добавляет отбор в набор отборов компоновщика или группы отборов
//
&НаСервереБезКонтекста
Функция ДобавитьОтбор(ЭлементСтруктуры, Знач Поле, Значение, ВидСравнения = Неопределено, Использование = Истина) Экспорт
	
	Если ТипЗнч(Поле) = Тип("Строка") Тогда
		Поле = Новый ПолеКомпоновкиДанных(Поле);
	КонецЕсли;
	
	Если ТипЗнч(ЭлементСтруктуры) = Тип("КомпоновщикНастроекКомпоновкиДанных") Тогда
		Отбор = ЭлементСтруктуры.Настройки.Отбор;
	Иначе
		Отбор = ЭлементСтруктуры;
	КонецЕсли;
	
	Если ВидСравнения = Неопределено Тогда
		ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	КонецЕсли;
	
	НовыйЭлемент = Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	НовыйЭлемент.Использование  = Использование;
	НовыйЭлемент.ЛевоеЗначение  = Поле;
	НовыйЭлемент.ВидСравнения   = ВидСравнения;
	НовыйЭлемент.ПравоеЗначение = Значение;
	Возврат НовыйЭлемент;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЭтоПериодическийОбъект(ОбъектПоиска, ИмяРеквизитаДата = "")
	Если ОбъектПоиска.Тип = "Документ" ИЛИ ОбъектПоиска.Тип = "БизнесПроцесс" Тогда
		Возврат Истина;
	ИначеЕсли ОбъектПоиска.Тип = "РегистрСведений" Тогда 
		ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(ОбъектПоиска.ПолноеИмя);
		Если ОбъектМетаданных.ПериодичностьРегистраСведений <> Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Непериодический Тогда
			Возврат Истина;
		КонецЕсли;
	Иначе
		ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(ОбъектПоиска.ПолноеИмя);
		Если ОбъектМетаданных.Реквизиты.Найти("Дата") <> Неопределено Тогда
			Если ОбъектМетаданных.Реквизиты.Дата.Тип.Типы().Количество() = 1
				И ОбъектМетаданных.Реквизиты.Дата.Тип.Типы()[0] = Тип("Дата") Тогда
				Возврат Истина;
			КонецЕсли;
		КонецЕсли;
		Если ОбъектМетаданных.Реквизиты.Найти("ДатаСоздания") <> Неопределено Тогда
			Если ОбъектМетаданных.Реквизиты.ДатаСоздания.Тип.Типы().Количество() = 1 И ОбъектМетаданных.Реквизиты.ДатаСоздания.Тип.Типы()[0] = Тип("Дата") Тогда
				ИмяРеквизитаДата = "ДатаСоздания";
				Возврат Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Возврат Ложь
КонецФункции // ()

&НаСервере
Функция ПолучитьРезультат(НачатьПоиск = Ложь)
	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("ТекстЗапроса", ТекстЗапроса);
	СтруктураРезультата.Вставить("ТекстПроизвольногоЗапроса", ТекстПроизвольногоЗапроса);
	СтруктураРезультата.Вставить("СтрокаПоиска", СтрокаПоиска);
	СтруктураРезультата.Вставить("СтрокаПоискаСписок", Элементы.СтрокаПоиска.СписокВыбора.ВыгрузитьЗначения());
	СтруктураРезультата.Вставить("Настройки", ОтборДанных.ПолучитьНастройки());
	СтруктураРезультата.Вставить("РежимОтбора", РежимОтбора);
	СтруктураРезультата.Вставить("ПараметрыЗапроса", ПараметрыЗапроса);
	СтруктураРезультата.Вставить("НачатьПоиск", НачатьПоиск);
	
	Возврат СтруктураРезультата;
КонецФункции

&НаСервере
Процедура ЗаполнитьНастройки(КомпоновщикНастроек, Настройки)
	
	СхемаКомпоновки = Новый СхемаКомпоновкиДанных();
    	
	Источник = СхемаКомпоновки.ИсточникиДанных.Добавить();
	Источник.Имя = "ИсточникДанных";
	Источник.СтрокаСоединения="";
	Источник.ТипИсточникаДанных = "local";
	
	НаборДанных = СхемаКомпоновки.НаборыДанных.Добавить(Тип("НаборДанныхЗапросСхемыКомпоновкиДанных"));
	НаборДанных.Запрос = ТекстЗапроса;
	НаборДанных.Имя = "НаборДанныхЗапроса";
	НаборДанных.ИсточникДанных = Источник.Имя;
	НаборДанных.АвтоЗаполнениеДоступныхПолей = Истина;
	
	Если ЭтоПериодическийОбъект(ОбъектПоиска) Тогда
		ПараметрСКД = СхемаКомпоновки.Параметры.Добавить();
		ПараметрСКД.Имя = "Период";
		ПараметрСКД.ТипЗначения = Новый ОписаниеТипов("СтандартныйПериод");
		ПараметрСКД.Использование = ИспользованиеПараметраКомпоновкиДанных.Авто;
		ПараметрСКД.ОграничениеИспользования = Ложь;
		ПараметрСКД.ВключатьВДоступныеПоля = Ложь;
		
		ПараметрСКД = СхемаКомпоновки.Параметры.Добавить();
		ПараметрСКД.Имя = "НачДата";
		ПараметрСКД.Использование = ИспользованиеПараметраКомпоновкиДанных.Авто;
		ПараметрСКД.ОграничениеИспользования = Истина;
		ПараметрСКД.ВключатьВДоступныеПоля = Ложь;
		ПараметрСКД.Выражение = "&Период.ДатаНачала";
			
		ПараметрСКД = СхемаКомпоновки.Параметры.Добавить();
		ПараметрСКД.Имя = "КонДата";
		ПараметрСКД.Использование = ИспользованиеПараметраКомпоновкиДанных.Авто;
		ПараметрСКД.ОграничениеИспользования = Истина;
		ПараметрСКД.ВключатьВДоступныеПоля = Ложь;
		ПараметрСКД.Выражение = "&Период.ДатаОкончания";
	КонецЕсли;
		
	АдресСхемы = ПоместитьВоВременноеХранилище(СхемаКомпоновки, УникальныйИдентификатор);
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемы));
	
	Если Настройки <> Неопределено Тогда
		КомпоновщикНастроек.ЗагрузитьНастройки(Настройки);
	КонецЕсли;
	КомпоновщикНастроек.Восстановить(СпособВосстановленияНастроекКомпоновкиДанных.Полное);
	КомпоновщикНастроек.Восстановить(СпособВосстановленияНастроекКомпоновкиДанных.ПроверятьДоступность);
	
	Если КомпоновщикНастроек.Настройки.Отбор.Элементы.Количество() = 0 Тогда
		Для каждого ЭлементДоступногоОтбора Из КомпоновщикНастроек.Настройки.ДоступныеПоляОтбора.Элементы Цикл    
			ДобавитьОтбор(КомпоновщикНастроек.Настройки.Отбор, ЭлементДоступногоОтбора.Поле, , ВидСравненияКомпоновкиДанных.Равно, Ложь);
		КонецЦикла; //Для каждого ЭлементДоступногоОтбора Из   
	КонецЕсли;
	
	Если КомпоновщикНастроек.Настройки.Порядок.Элементы.Количество() = 0 Тогда
		ПолеДата = Новый ПолеКомпоновкиДанных("Дата");
		Для каждого ЭлементДоступногоПорядка Из КомпоновщикНастроек.Настройки.ДоступныеПоляПорядка.Элементы Цикл    
			НовыйПорядок = КомпоновщикНастроек.Настройки.Порядок.Элементы.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
		    НовыйПорядок.Использование = (ЭлементДоступногоПорядка.Поле = ПолеДата);
		    НовыйПорядок.Поле = ЭлементДоступногоПорядка.Поле;
		    НовыйПорядок.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр;
		КонецЦикла; //Для каждого ЭлементДоступногоПорядка Из   
	КонецЕсли;
	
	Если КомпоновщикНастроек.Настройки.Выбор.Элементы.Количество() = 0 Тогда
		НовыйЭлемент = КомпоновщикНастроек.Настройки.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
		НовыйЭлемент.Использование = Истина;
		НовыйЭлемент.Поле = Новый ПолеКомпоновкиДанных("СистемныеПоля.НомерПоПорядку"); //SystemFields.GroupSerialNumber
		
		Для каждого ЭлементДоступногоВыбора Из КомпоновщикНастроек.Настройки.ДоступныеПоляВыбора.Элементы Цикл    
			Если НЕ ЭлементДоступногоВыбора.Папка Тогда
				Если ИменаПолейТЧ.НайтиПоЗначению(ЭлементДоступногоВыбора.Поле) = Неопределено Тогда
					НовыйЭлемент = КомпоновщикНастроек.Настройки.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
				    НовыйЭлемент.Использование = Истина;
				    НовыйЭлемент.Поле = ЭлементДоступногоВыбора.Поле;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла; //Для каждого ЭлементДоступногоВыбора Из   
		
		Если ИменаПолейТЧ.Количество() > 0 Тогда
			
			ГруппаЭлементов = КомпоновщикНастроек.Настройки.Выбор.Элементы.Добавить(Тип("ГруппаВыбранныхПолейКомпоновкиДанных"));
			ГруппаЭлементов.Использование = Истина;
			ГруппаЭлементов.Заголовок = НСтр("ru = 'Агрегатные поля'");
			
			Для каждого ЭлементМассива Из ИменаПолейТЧ Цикл    
				Если КомпоновщикНастроек.Настройки.ДоступныеПоляВыбора.НайтиПоле(ЭлементМассива.Значение) <> Неопределено Тогда
					НовыйЭлемент = ГруппаЭлементов.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
				    НовыйЭлемент.Использование = Истина;
				    НовыйЭлемент.Поле = ЭлементМассива.Значение;
				КонецЕсли;
			КонецЦикла; //Для каждого ЭлементМассива Из  
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗаполнитьПараметрыЗапроса()
	
	Если ПустаяСтрока(ТекстПроизвольногоЗапроса) Тогда
		Возврат "Отсутствует текст запроса.";
	КонецЕсли;
	
	Запрос = Новый Запрос(ТекстПроизвольногоЗапроса);
	Попытка
		ПараметрыВЗапросе = Запрос.НайтиПараметры();
	Исключение
		Возврат ОписаниеОшибки();
	КонецПопытки;
	
	Для каждого ПараметрЗапроса Из ПараметрыВЗапросе Цикл
		ИмяПараметра =  ПараметрЗапроса.Имя;
		СтрокаПараметров = ПараметрыЗапроса.НайтиСтроки(Новый Структура("ИмяПараметра", ИмяПараметра));
		Если  СтрокаПараметров.Количество() = 0 Тогда
			СтрокаПараметров = ПараметрыЗапроса.Добавить();
			СтрокаПараметров.ИмяПараметра = ИмяПараметра;
		Иначе 
			СтрокаПараметров = СтрокаПараметров[0];
		КонецЕсли;
		
		СтрокаПараметров.ЗначениеПараметра = ПараметрЗапроса.ТипЗначения.ПривестиЗначение(СтрокаПараметров.ЗначениеПараметра);
	КонецЦикла;
	
	Возврат Истина;
КонецФункции

&НаСервере
Процедура УстановитьВидимостьДоступность()
	Если РежимОтбора = 1 Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ПроизвольныйЗапрос;
	Иначе
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ОтборПоЗначениямРеквизитов;
		Если НЕ ЭтоПериодическийОбъект(ОбъектПоиска) Тогда
			Элементы.ОтборДанныхНастройкиПараметрыДанных.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ТекстЗапроса = Параметры.ТекстЗапроса;
	ТекстПроизвольногоЗапроса = Параметры.ТекстПроизвольногоЗапроса;
	СтрокаПоиска = Параметры.СтрокаПоиска;
	Если Параметры.Свойство("СтрокаПоискаСписок") Тогда
		Элементы.СтрокаПоиска.СписокВыбора.ЗагрузитьЗначения(Параметры.СтрокаПоискаСписок);
	КонецЕсли;
	
	Элементы.РежимОтбора.СписокВыбора.Добавить(0, "Отбор по реквизитам");
	Элементы.РежимОтбора.СписокВыбора.Добавить(1, "Произвольный запрос");
	
	РежимОтбора = Параметры.РежимОтбора;
	ПараметрыЗапроса.Загрузить(Параметры.ПараметрыЗапроса.Выгрузить());
	
	ИменаПолейТЧ = Параметры.ИменаПолейТЧ;
	
	ОбъектПоиска = Параметры.ОбъектПоиска;
	
	Заголовок = Заголовок + " [" + ОбъектПоиска.Представление + "]";
	
	Настройки = Параметры.Настройки;
	ЗаполнитьНастройки(ОтборДанных, Настройки);
	
	УстановитьВидимостьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	#Если ТолстыйКлиентУправляемоеПриложение  Тогда
	Элементы.КонтекстноеМенюТекстЗапросаКонструкторЗапроса.Доступность = Истина;
	#КонецЕсли
КонецПроцедуры

&НаКлиенте
Процедура ПараметрыЗапросаЗначениеПараметраОчистка(Элемент, СтандартнаяОбработка)
	Элемент.ВыбиратьТип = Истина;
КонецПроцедуры

&НаКлиенте
Процедура РежимПоискаПриИзменении(Элемент)
	УстановитьВидимостьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура СтрокаПоискаПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(СтрокаПоиска) Тогда
		СписокВыбора = Элементы.СтрокаПоиска.СписокВыбора;
		ПозицияСтроки = СписокВыбора.НайтиПоЗначению(СтрокаПоиска);
		Если ПозицияСтроки = Неопределено Тогда
			СписокВыбора.Вставить(0, СтрокаПоиска);
		Иначе
			СписокВыбора.Сдвинуть(ПозицияСтроки, - СписокВыбора.Индекс(ПозицияСтроки));
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПрименитьОтбор(Команда)
	
	Оповестить("ПрименитьОтборУниверсальныйПодбор", ПолучитьРезультат(Ложь), ЭтаФорма);
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура НайтиОбъекты(Команда)
	
	Оповестить("ПрименитьОтборУниверсальныйПодбор", ПолучитьРезультат(Истина), ЭтаФорма);
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура КонструкторЗапроса(Команда)
	#Если ТолстыйКлиентУправляемоеПриложение  Тогда
	КонструкторЗапроса = Новый КонструкторЗапроса;
	
	Если НЕ ПустаяСтрока(ТекстПроизвольногоЗапроса) Тогда 
		КонструкторЗапроса.Текст = ТекстПроизвольногоЗапроса;
	КонецЕсли;
	
	Если КонструкторЗапроса.ОткрытьМодально() Тогда 
		ТекстПроизвольногоЗапроса = КонструкторЗапроса.Текст;
	КонецЕсли;
	#КонецЕсли
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПараметры(Команда)
	Результат = ЗаполнитьПараметрыЗапроса();
	Если Результат <> Истина Тогда
		Предупреждение(Результат, 60, "Ошибка!");
	КонецЕсли;
КонецПроцедуры